BBall[] bball = new BBall[150];

void setup() {
  size(window.innerWidth, window.innerHeight, P3D);
  for (int i = 0; i < bball.length; ++i) {
    bball[i] = new BBall();
  }
  background(0);
}

float t = 0;
void draw() {
  background(0);
  ambientLight(102, 102, 102);
  lightSpecular(204, 204, 204);
  directionalLight(102, 102, 102, 0, 0, -1);
  specular(255, 255, 255);
  for (int i = 0; i < bball.length; ++i) {
    bball[i].run();
  }
  translate(width/2, height/2, 0);
  fill(127*hour()/24, 127*minute()/60, 127*second()/60);
  textSize(40);
  pushMatrix();
  rotateZ(PI*cos(radians(10*t))/12);
  rotateY(PI*sin(radians(15*t))/18);
  rotateX(PI*cos(radians(5*t))/18);
  text(hour()+":"+nf(minute(), 2), -56, 10, 50*cos(radians(3*t)));
  popMatrix();
  rotate(radians(t), sin(radians(t)), -sin(radians(t)), cos(radians(t)));
  noStroke();
  shininess(6.0);
  for (int i = 0; i <= 60; ++i) {
    fill(255);
    pushMatrix();
    rotateZ(radians(i*6-90));
    translate(width/5, 0, 0);
    sphere(2);
    popMatrix();
  }
  for (int i = 0; i <= second (); ++i) { 
    fill(255*hour()/24, 255*second()/60, 255*minute()/60);
    pushMatrix();
    rotateZ(radians(i*6-90));
    translate(width/5, 0, 0);
    sphere(4);
    popMatrix();
  }
  ++t;
}

class BBall {
  float[] x = new float[3];
  float v;
  BBall() {
    x[0] = random(0,window.innerWidth);
    x[1] = random(0,window.innerHeight);
    x[2] = random(-5000, 0);
    v = random(100, 300);
  }
  void move() {
    x[2] = (x[2]-v)%5000;
  }
  void display() {
    pushMatrix();
    translate(x[0], x[1], x[2]);
    noStroke();
    fill(105, 105, 0);
    shininess(10.0);
    sphere(10);
    popMatrix();
  }
  void run() {
    move();
    display();
  }
}
